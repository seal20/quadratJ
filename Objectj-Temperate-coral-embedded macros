//Short cut and menu entry for specific marker. You need to modify these if the object list were modify.
macro "Healthy Coral [1]"{ojSwitchToItem("healthy_coral");}
macro "Bleached Coral [2]"{ ojSwitchToItem("bleached_coral");}
macro "Dead Coral [3]"{ ojSwitchToItem("dead_coral");}
macro "Branched coralline algae[q]"{ojSwitchToItem("branched_coralline_algae");}
macro "Crustose Coralline algae [w]"{ojSwitchToItem("crustose_coralline_algae");}
macro "Obamoku [e]"{ojSwitchToItem("Obamoku_Sargassum_ringoldianum");}
macro "Makusa [a]"{ojSwitchToItem("Makusa_Gelidium_sp");}
macro "Fukuronori [s]"{ojSwitchToItem("Fukuronori_Colpomenia_sinuosa");}
macro "Akamoku [d]"{ojSwitchToItem("Akamoku_Sargassum_horneri");}
macro "Kajime [z]"{ojSwitchToItem("Kajime_Ecklonia_cava");}
macro "Arame [x]"{ojSwitchToItem("Arame_Eisenia_bicyclis");}
macro "Algae other [c]"{ojSwitchToItem("Algae_other");}
macro "Bare substrate [4]"{ojSwitchToItem("bare_substrate");}
macro "Off limit [5]"{ojSwitchToItem("off");}
macro "Umiuchiwa [r]"{ojSwitchToItem("Umiuchiwa_Padina_arborescens");}
macro "Wakame [f]"{ojSwitchToItem("Wakame_Undaria");}


//Prepare some variable for later
//Prepare variable for file name
dir=getDirectory("image");
name = getTitle() + "PointCoordinates.txt";
//check if file exists
check = File.exists(dir+name);

//adding, hiding an removing labels and point
macro "Update Initials [7]"{ 
	updateMyLabels(false);
}

macro "Update Labels [8]"{
	updateMyLabels(true);
}


macro "Hide Labels [9]"{
	run("Hide Overlay");
}


function updateMyLabels(wholeStr){
	fontSize = 18;
	first = true;
	for (obj = 1; obj <= ojNObjects(); obj++){
		if (ojOwnerIndex(obj) == ojImageLink()){
			ojSelectObject(obj);
			slc = ojZPos(1);
			x = ojXPos(1);
			y = ojYPos(1);
			if (getSliceNumber == slc){
				if (first)
					run("Remove Overlay");
				first = false;
				makeOval(x-10, y-10, 20, 20);
				s = ojGetItemName();
				color = "black";
				if (!wholeStr) s = substring(s, 0, 1);
				makeText(s, x + 4, y-fontSize/2);
				run("Properties... ", "name=[] position=none stroke=&color font="+fontSize+" fill=none");
				run("Add Selection...");
				run("Select None");
			}
		}
	}
	ojSelectObject(0);
}

macro "Clear all to start again"{
	run("Remove Overlay");
	//Prepare variable for file name
	dir=getDirectory("image");
	name = getTitle() + "PointCoordinates.txt";
	//check if file exists
	check = File.exists(dir+name);
//	itemNames = split(ojGetItemNames());
//	nTypes = itemNames.length;
//	for (m=1; m < nTypes; m++){ojDeleteItem(nTypes, m);
//	}
//	ojDeleteObject(1)
	if (check == 1){
	    getDateAndTime(year, month, dayOfWeek, dayOfMonth, hour, minute, second, msec);
	    time = (year + month + dayOfMonth + hour+minute+second);
	    File.rename(dir+name,dir+name+time);
	}
}   

//Produce the count Results
macro "Show Count Results"{
	run("Clear Results");
	itemNames = split(ojGetItemNames());
	maxImg = ojNImages();
	nTypes = itemNames.length;
	totalCounts = newArray(nTypes);
	for (img = 1; img <= maxImg; img++){
		fname  = ojGetImageName(img);
		itemCounts = newArray(nTypes);
		setResult("Label", img-1, fname);
		for (obj = ojFirstObject(img); obj <= ojLastObject(img); obj++){
			ojSelectObject(obj);
			
			for (kk = 0; kk < nTypes; kk++){
				name = ojGetItemName();
				if (name == itemNames[kk]){
					itemCounts[kk]++;
					totalCounts[kk]++;
				}
			}	
		}
		for (kk = 0; kk < nTypes; kk++)
			setResult(itemNames[kk], img - 1, itemCounts[kk]);
	}
	setResult("Label", maxImg, "__Total__");
	for (kk = 0; kk < nTypes; kk++)
		setResult(itemNames[kk], maxImg, totalCounts[kk]);
	updateResults;
	ojSelectObject(0);
	selectWindow("Results");
}

//*****************
//Draw random points
//Ask the user for the number of point, default 64
var n=50
macro "Set the number of points (default 50) [n]"{
	Dialog.create("Enter the number of points ");
	Dialog.addNumber("Number",n);
	Dialog.show();
	n = Dialog.getNumber();
}

// draw the number of points defined earlier
macro "Draw Random Points [0]"{
	Overlay.remove;
	dotSize = 25;
	setLineWidth(5);
	setColor(255,0,0);
	width = getWidth();
	height = getHeight();
	//Prepare variable for file name
	dir=getDirectory("image");
	name = getTitle() + "PointCoordinates.txt";
//check if coordinate file exists
	check = File.exists(dir+name);
	if (check == 0){
	  f = File.open(dir+name);
	  for (j=0; j<(n*n); j++) {
	  x = random()*width-dotSize/2;
	  X=abs(x);
	  y = random()*height-dotSize/2;
	  Y=abs(y);
	  print(f,X+ "\t" + Y);
	  Overlay.drawEllipse(X, Y, dotSize, dotSize);
	}
	File.close(f);
	} else {
	  filestring=File.openAsString(dir+name);
	  rows=split(filestring, "\n");
	  x=newArray(rows.length);
	  y=newArray(rows.length);
	  for(ii=0; ii<rows.length; ii++){
	    columns=split(rows[ii],"\t");
	    x[ii]=parseInt(columns[0]);
	    y[ii]=parseInt(columns[1]);
	    Overlay.drawEllipse(x[ii], y[ii], dotSize, dotSize);
	  } 
	}
	Overlay.show;
}

//*****************
//Draw a grid


macro "Draw points on a grid [-]"{
//Prepare to Draw drawEllipse
	Overlay.remove;
	dotSize = 25;
	setLineWidth(5);
	setColor(255,0,0);
//read the position of the quadrats from user input
	run("Select None");
	waitForUser("Draw a polygon of your quadrats using the polygon tools and then press ok");
	getSelectionCoordinates(x, y);
	tlx=x[0];
	tly=y[0];
	trx=x[1];
	try=y[1];
	brx=x[2];
	bry=y[2];
	blx=x[3];
	bly=y[3];
//calculate the equation describing the top frame
	slopeTop = (try - tly)/(trx-tlx);
	interceptTop = tly-(slopeTop*tlx);
//	print("Top",slopeTop,interceptTop);
//calculate the equation describing the right frame
	slopeRight = (bry-try)/(brx-trx);
	interceptRight = try-(slopeRight*trx);
//	print("Right",slopeRight,interceptRight);
//calculate the equation describing the bottom frame
	slopeBottom = (bry-bly)/(brx-blx);
	interceptBottom = bly-(slopeBottom*blx);
//	print("Bottom",slopeBottom,interceptBottom);
//calculate the equation describing the left frame
	slopeLeft= (bly-tly)/(blx-tlx);
	interceptLeft = tly-(slopeLeft*tlx);
//	print("Left",slopeLeft,interceptLeft);
//determine the crossing point of the top and bottom (horizontal) tbx and tby
	tbx=(interceptBottom-interceptTop)/(slopeTop-slopeBottom);
	tby=slopeBottom*tbx+interceptBottom;
//	print(tbx,tby);
//determine the crossing point of the left and right lrx and lry
	lrx=(interceptRight-interceptLeft)/(slopeLeft-slopeRight);
	lry=slopeLeft*tbx+interceptLeft;
//	print(lrx,lry);
//determine the difference between the intercept of the top and bottom frame and divide by 9 to get 8 interval on the vertical 
//store the results in an array
	xInterval = newArray(8);
	horizontalShift=((-interceptRight/slopeRight)+(interceptLeft/slopeLeft))/9;
//	print(horizontalShift);
	for (i=0;i<8;i++){
	  xInterval[i]=(0-interceptLeft/slopeLeft)+horizontalShift*(i+1);
//	  print("xInterval",xInterval[i]);  
	}
	Array.show(xInterval);
//determine the difference between the intercept of the left and right frame and divide by 9 to get 8 interval on the horizontal 
	yInterval= newArray(8);
	verticalShift=(interceptBottom-interceptTop)/9;
	for (j=0;j<8;j++){
	  yInterval[j]=interceptBottom-verticalShift*(j+1);
//	  print("yInterval", yInterval[j]);
	}
//	Array.show(yInterval);
//determin the equation of all horizontal line
	horizontalSlope=newArray(8);
	horizontalIntercept=newArray(8);
	for (k=0;k<8;k++){
	  horizontalSlope[k]=(tby-yInterval[k])/tbx;
	  horizontalIntercept[k]=tby-horizontalSlope[k]*tbx;
//	  print("horizontalSlope",horizontalSlope[k]);
///	  print("horizontalIntercept",horizontalIntercept[k]);
	}
//	Array.show(horizontalSlope);
//	Array.show(horizontalIntercept);
//determin the equation of all vertical line
	verticalSlope=newArray(8);
	verticalIntercept=newArray(8);
	for (l=0;l<8;l++){
	  verticalSlope[l]=(lry)/(lrx-xInterval[l]);
	  verticalIntercept[l]=lry-verticalSlope[l]*lrx;
//	  print("verticalSlope",verticalSlope[l]);
//	  print("verticalIntercept",verticalIntercept[l]);
	}
	Array.show(verticalSlope);
	Array.show(verticalIntercept);
//Determine the crossing point of each line
	for (ii=0;ii<8;ii++){
	  for (jj=0;jj<8;jj++){
	    x=-(horizontalIntercept[jj]-verticalIntercept[ii])/(horizontalSlope[jj]-verticalSlope[ii]);
	    y=horizontalSlope[jj]*x+horizontalIntercept[jj];
//	    print(x,y);
	    Overlay.drawEllipse(x, y, dotSize, dotSize);
	  }
	}
Overlay.show;     
}  

//May be goo to shift to a function some day, something like this
//function DetermineEquation(slope,intercept,x0,y0,yi){
//	slope[i]=(y0-yi)/(x0);
//	intercept[i] = y0 -(slope[i]*x0);
//}